class_name UpgradeManager
extends Node

@export var enemy_manager: EnemyManager
@export var spawn_position: Marker2D
@export var spawn_root: Node2D
@export var available_upgrades: Array[UpgradeResource]

var upgrade_option_scene: PackedScene = preload("uid://egb6it4cxmj6")
var peer_id_to_upgrade_options: Dictionary[int, Array] = {}


func _ready() -> void:
	enemy_manager.round_completed.connect(_on_round_completed)


## Server-side only. To propagate upgrade options to clients, set_upgrade_options must
## be rpc'd.
func generate_upgrade_options() -> void:
	peer_id_to_upgrade_options.clear()
	var connected_peer_ids := multiplayer.get_peers()
	connected_peer_ids.append(MultiplayerPeer.TARGET_PEER_SERVER)
	
	for connected_peer_id in connected_peer_ids:
		peer_id_to_upgrade_options[connected_peer_id] = [
			available_upgrades[0],
			available_upgrades[0],
			available_upgrades[0]
		]
		var upgrade_resources: Array[UpgradeResource] = [
			available_upgrades[0],
			available_upgrades[0],
			available_upgrades[0]
		]
		
		var upgrade_options := create_upgrade_option_nodes(upgrade_resources)
		var selected_upgrades: Array[Dictionary] = []
		for i in upgrade_options.size():
			var upgrade_option := upgrade_options[i]
			var upgrade_resource := upgrade_resources[i]
			upgrade_option.set_peer_id_filter(connected_peer_id)
			# Create a unique node name for each upgrade option that will keep the
			# tree path in sync across clients without conflict of same name
			var uid := ResourceUID.create_id()
			upgrade_option.name = str(uid)
			
			selected_upgrades.append({
				"name": upgrade_option.name,
				"id": upgrade_resource.id
			})
			
			# Hides peer nodes from the host while keeping them in the tree.
			upgrade_option.visible = connected_peer_id == MultiplayerPeer.TARGET_PEER_SERVER
		
		if connected_peer_id != MultiplayerPeer.TARGET_PEER_SERVER:
			set_upgrade_options.rpc_id(connected_peer_id, selected_upgrades)


func create_upgrade_option_nodes(upgrade_resources: Array[UpgradeResource]) -> Array[UpgradeOption]:
	
	var result: Array[UpgradeOption] = []
	var initial_x: int = -64
	var x_difference: int = 64
	
	for i in range(upgrade_resources.size()):
		var upgrade_option := upgrade_option_scene.instantiate() as UpgradeOption
		upgrade_option.set_upgrade_index(i)
		upgrade_option.set_upgrade_resource(upgrade_resources[i])
		
		upgrade_option.global_position = spawn_position.global_position
		# Spreads 3 upgrades evenly across the arena.
		upgrade_option.global_position += Vector2.RIGHT * (initial_x + (x_difference * i))
		spawn_root.add_child(upgrade_option)
		
		upgrade_option.selected.connect(_on_upgrade_option_selected)
		result.append(upgrade_option)

	return result


@rpc("authority", "call_local", "reliable")
func set_upgrade_options(selected_upgrades: Array[Dictionary]) -> void:
	var upgrade_resources: Array[UpgradeResource] = []
	for upgrade in selected_upgrades:
		var resource_index := available_upgrades.find_custom(func (item: UpgradeResource):
			return item.id == upgrade.id
		)
		upgrade_resources.append(available_upgrades[resource_index])
	
	var created_nodes := create_upgrade_option_nodes(upgrade_resources)
	for i in created_nodes.size():
		created_nodes[i].name = selected_upgrades[i].name


func handle_upgrade_selected(upgrade_index: int, for_peer_id: int) -> void:
	print("Peer %s has selected upgrade with id %s" %[
		for_peer_id,
		peer_id_to_upgrade_options[for_peer_id][upgrade_index].id
	])


func _on_round_completed() -> void:
	generate_upgrade_options()


func _on_upgrade_option_selected(upgrade_index: int, for_peer_id: int) -> void:
	handle_upgrade_selected(upgrade_index, for_peer_id)
